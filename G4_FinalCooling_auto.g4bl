# Simulates Final Cooling Wedge

# Created by: D. Stratakis 
# Modified by Daniel Fu for scripted running
# Updated: September 20, 2023

# COLORS:  100 red, 010 green, 111 white, 110 yellow, 001 blue, 101 magenta, 011 cyan

# VERSION HISTORY

# v304: Creates distributions at entrance of Q20 and Q21

######################### DEFINE PHYSICS MODEL
 
# randomseed time
  
  param zbegin=0.0
  start initialZ=$zbegin  y=0.000   x=0.00   z=0.00    radiusCut=300
  param steppingFormat=N,GLOBAL,CL,STEP,VOL,PROCESS,P,KE,POLAR,B
  param fieldVoxels=400,400,400 
  param maxStep=0.5
  
  param -unset minRangeCut=1

  physics QGSP_BERT doStochastics=1 spinTracking=1 synchrotronRadiation=1 minRangeCut=$minRangeCut # disable=Decay
  # physics FTFP_BERT doStochastics=1 spinTracking=1 synchrotronRadiation=1 # disable=Decay

  trackcuts keep=mu-,mu+

  particlecolor proton=1,1,1 
  particlecolor pi+=0,1,0 
  particlecolor mu+=1,0.5,0   # orange
  particlecolor mu-=0,1,0   # green
  particlecolor e+=1,0,0      # red
  particlecolor gamma=0,0,1   # blue
  particlecolor e-=1,0.5,0    # orange

######################### DEFINE INPUT BEAM

  param -unset nparticles=50000

  param -unset beamfile=particles_before.txt

  beam ascii filename=$beamfile nEvents=$nparticles beamZ=0  
                                   
######################### DEFINE WEDGE

  material Diamond1 C,1.0 density=3.51  

  param pi=3.141592654
  param degrad=$pi/180

  param -unset abshgt=10
  param abswidth=100
  param -unset absLEN3=18.0
  param -unset abshalfangle3=45.0 
  param -unset absoffset3=3.2     
  
  param wedge_z=0.5*$absLEN3
     
  extrusion LHextrDS3 color=1,0,1 material=Diamond1 length=$abswidth maxStep=0.1\
  vertices='0,0; \
                 -$absLEN3/2,$absLEN3/2/tan($abshalfangle3*$degrad); \
                 -$absLEN3/2,$abshgt; \
                 0,$abshgt'

  extrusion LHextrUS3 color=1,0,1 material=Diamond1 length=$abswidth maxStep=0.1\
  vertices='0,0; \
                  0,$abshgt; \
                  $absLEN3/2,$abshgt; \
                  $absLEN3/2,$absLEN3/2/tan($abshalfangle3*$degrad)'

######################### DEFINE VIRTUAL SCREENS

  param VDRad=60.0
  virtualdetector VD    radius=$VDRad    length=0.2  color=1,1,1 format=asciiExtended coordinates=centerline
   
##########################################################################################################
############################################  PLACE ELEMENTS  ############################################
##########################################################################################################
  
# place VD             front=1 rename=VD_Diagnostic_01         z=1.0

  param -unset wedgeAxis=0  # 1 (or any nonzero) indicates y axis
  param -unset noWedge=0  # 1 to completely remove the wedge
  
  if $noWedge==0
    if $wedgeAxis==1
      place LHextrDS3 x=0 y=$absoffset3 z=$wedge_z rotation=Z180,Y90,X00
      place LHextrUS3 x=0 y=$absoffset3 z=$wedge_z+0 rotation=Z180,Y90,X00
    else
      place LHextrDS3 x=$absoffset3 y=0 z=$wedge_z rotation=Z90,Y00,X90
      place LHextrUS3 x=$absoffset3 y=0 z=$wedge_z+0 rotation=Z90,Y00,X90
    endif
  endif


  param -unset outname=particles_after

  place VD             front=1 rename=$outname        z=$absLEN3+1
 
  # place VD             front=1 rename=particles_End          z=20.0 
